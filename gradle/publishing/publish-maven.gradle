apply plugin: 'maven-publish'

publishing.publications.all {
    def devs = ['johanhaleby:Johan Haleby:johan.haleby at jayway.com', 'jakr:Jan Kronquist:jan.kronquist at jayway.com', 'thekingnothing:Arthur Zagretdinov:arthur.zagretdinov at outlook.com']

    pom.withXml {
        def root = asNode()
        root.remove(root.get('name'))
        root.appendNode('name', 'PowerMock')

        root.remove(root.get('url'))
        root.appendNode('url', 'http://www.powermock.org')

        root.remove(root.get('licenses'))
        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode('name', 'The Apache Software License, Version 2.0')
        license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
        license.appendNode('distribution', 'repo')

        def developers = root.appendNode('developers')
        devs.each {
            def split = it.split(':')
            assert split.length == 3
            def d = developers.appendNode('developer')
            d.appendNode('id', split[0])
            d.appendNode('name', split[1])
            d.appendNode('email', split[2])
        }

        root.dependencies.'*'.findAll() {
            it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                dep.name == it.artifactId.text()
            }
        }.each() {
            it.scope*.value = 'compile'
        }

        def depends
        if (root.dependencies.size() > 0) {
            depends = root.dependencies.get(0)
        }else{
            depends = root.appendNode('dependencies')
        }

        if (project.configurations.hasProperty('provided')) {
            project.configurations.provided.allDependencies.each {
                def dep = depends.appendNode('dependency')
                dep.appendNode('groupId', it.group)
                dep.appendNode('artifactId', it.name)
                dep.appendNode('version', it.version)
                dep.appendNode('scope', 'provided')
            }
        }

        if (project.configurations.hasProperty('mockito2')){
            project.configurations.mockito2.allDependencies.each {
                def dep = depends.appendNode('dependency')
                dep.appendNode('groupId', it.group)
                dep.appendNode('artifactId', it.name)
                dep.appendNode('version', it.version)
                dep.appendNode('scope', 'compile')
            }
        }

    }
}


